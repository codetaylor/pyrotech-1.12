buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

ext.configFile = file "build.properties"
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

// ----------------------------------------------------------------------------

def getVersionName = { String workTree ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', "--git-dir=\"${workTree}/.git\"", "--work-tree=\"${workTree}\"", 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

project.ext.modVersion = getVersionName("${project.projectDir}")

project.ext.dependencies = ',dependencies = "' +
        'required-after:dropt@[' + config.droptVersion + ',);' +
        'required-after:athenaeum@[' + config.athenaeumVersion + ',);' +
        'after:patchouli;' +
        'after:crafttweaker;' +
        'after:jei;' +
        'after:gamestages;' +
        'after:thermalexpansion;' +
        '"'

version = "${config.minecraftVersion}-${project.ext.modVersion}"
group = config.packageGroup
archivesBaseName = config.modId

compileJava.doFirst {
    project.sourceSets.main.java.srcDirs.each { dir ->
        copy {
            from dir
            into "${project.buildDir}/modifiedSrc"
            include "**/*.java"
            filter {
                it.replaceAll('@@VERSION@@', project.version)
            }
            filter {
                it.replaceAll('//@@DEPENDENCIES@@', project.ext.dependencies)
            }
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        }
        println "   [OK] Preprocessed [${dir}]"
    }
}
compileJava.doFirst {
    source = "${project.buildDir}/modifiedSrc"
    classpath = project.sourceSets.main.compileClasspath
    //destinationDir = file("${project.buildDir}/classes")
}
compileJava.doLast {
    println "   [OK] Compiled [${project.name}]"
}

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "${config.minecraftVersion}-${config.forgeVersion}"
    runDir = "run"
    mappings = config.mappings
}

repositories {

    // hwyla
    maven { url "http://tehnut.info/maven" }

    // patchouli
    maven { url 'https://maven.blamejared.com' }

    maven { // TOP
        name 'tterrag maven'
        url "http://maven.tterrag.com/"
    }

    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile project(":athenaeum")
    compile project(":dropt")
    deobfCompile "mcp.mobius.waila:Hwyla:${config.hwylaVersion}"
    deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:${config.topVersion}"
    compile "vazkii.patchouli:Patchouli:${config.patchouliVersion}"
}

jar {
    from sourceSets.api.output
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

/**
 * This task creates the wrapper files.
 */
task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}